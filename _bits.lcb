/*

Bit packing -- converting integers to byte arrays.  This (rather
horrible) module will go away once LCB provides its own syntax for
doing this.

*/

module _._bits

use com.livecode.foreign
use _._util

public handler __u_pack_bytes(in pVal as List/*<Number>*/) returns Data
	return pack_bytes(pVal)
end handler

public handler __u_pack_int32(in pVal as Int32) returns Data
	return pack_int(pVal, 4)
end handler
public handler __u_unpack_int32(in pData as Data) returns Int32
	return unpack_int(pData, 4)
end handler

-- FIXME LCB doesn't have an Int16 type yet.
public handler __u_pack_int16(in pVal as Int32) returns Data
	return pack_int(pVal, 2)
end handler
public handler __u_unpack_int16(in pData as Data) returns Int32
	return unpack_int(pData, 2)
end handler

----------------------------------------------------------------

private handler pack_bytes(in pVal as List) returns Data
	variable tResult as Data
	variable tByteNum as Number
	repeat for each element tByteNum in pVal
		put the byte with code tByteNum after tResult
	end repeat
	return tResult
end handler

private handler pack_int(in pVal as Number, in pCount as Number) returns Data

	variable tResult as Data
	put the empty data into tResult

	variable tByte as Number
	variable tByteNum as Number
	repeat with tByteNum from 0 up to (pCount - 1)
		put (pVal shifted right by (tByteNum * 8) bitwise) \
			bitwise and 255 into tByte

		put the byte with code tByte after tResult
	end repeat

	return tResult
end handler

----------------------------------------------------------------

private handler unpack_int(in pData as Data, in pCount as Number) \
		returns Number

	variable tResult as Number
	put 0 into tResult

	-- Check whether this is going to end up as a negative number.  If
	-- so, build it with bits inverted to avoid overflowing 32-bit
	-- integer when shifting
	variable tNegative as Boolean
	put ((the code of byte (pCount - 1) of pData) \
			bitwise and 128 > 0) into tNegative

	variable tByte as Number
	variable tByteNum as Number
	repeat with tByteNum from 0 up to (pCount - 1)
		put the code of byte (tByteNum + 1) of pData into tByte

		if tNegative then
			put (bitwise not tByte bitwise and 255) into tByte
		end if

		put tResult bitwise or \
			(tByte shifted left by (tByteNum * 8) bitwise) into tResult
	end repeat

	if tNegative then
		put bitwise not tResult into tResult
	end if

	return tResult
end handler

end module
