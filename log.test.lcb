module _.tests.log

use _.log0

variable sCapturedLogs as List

handler TestCustomHandler_Callback(in pMessage as String, \
		in pLevel as Number) returns nothing
	push [pLevel, pMessage] onto sCapturedLogs
end handler

public handler TestCustomHandler()
	-- Install a custom handler
	variable sOldHandler as ULogHandler
	put u_set_default_log_handler(TestCustomHandler_Callback) \
			into sOldHandler

	-- FIXME cannot compare handlers in LCB
	-- test "start with default handler" \
	--		when sOldHandler is U_DefaultLogHandler

	U_Warning("Blah")

	test "custom log handlers work" \
			when sCapturedLogs is [[4, "Blah"]]
end handler

public handler TestFormat()
	u_set_default_log_handler(TestCustomHandler_Callback)

	U_Info(["some {1}", "stuff"])
	test "format" when sCapturedLogs is [[6, "some stuff"]]
	put [] into sCapturedLogs

	-- Check that a warning is generated when the parameter is bad
	U_Info([])
	test "format (no format)" when sCapturedLogs[1][1] is 4

	U_Info([5])
	test "format (no format)" when sCapturedLogs[2][1] is 4

	U_Info({})
	test "format (non-list)" when sCapturedLogs[3][1] is 4
end handler

end module
